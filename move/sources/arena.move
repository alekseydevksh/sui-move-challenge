module challenge::arena;

use challenge::hero::Hero;
use sui::event;

// ========= STRUCTS =========

public struct Arena has key, store {
    id: UID,
    warrior: Hero,
    owner: address,
}

// ========= EVENTS =========

public struct ArenaCreated has copy, drop {
    arena_id: ID,
    timestamp: u64,
}

public struct ArenaCompleted has copy, drop {
    winner_hero_id: ID,
    loser_hero_id: ID,
    timestamp: u64,
}

// ========= FUNCTIONS =========

public fun create_arena(hero: Hero, ctx: &mut TxContext) {
    let arena = Arena {
        id: object::new(ctx),
        warrior: hero,
        owner: ctx.sender(),
    };

    event::emit(ArenaCreated {
        arena_id: object::id(&arena),
        timestamp: ctx.epoch_timestamp_ms()
    });

    transfer::share_object(arena);
}

#[allow(lint(self_transfer))]
public fun battle(hero: Hero, arena: Arena, ctx: &mut TxContext) {
    let Arena {
        id,
        warrior,
        owner,
    } = arena;

    let hero_id: ID = object::id(&hero);
    let warrior_id: ID = object::id(&warrior);
    let winner_id: ID;
    let loser_id: ID;

    if (hero.hero_power() >= warrior.hero_power()) {
        transfer::public_transfer(warrior, ctx.sender());
        transfer::public_transfer(hero, ctx.sender());
        winner_id = hero_id;
        loser_id = warrior_id;
    } else {
        transfer::public_transfer(warrior, owner);
        transfer::public_transfer(hero, owner);
        winner_id = warrior_id;
        loser_id = hero_id;
    };

    event::emit(ArenaCompleted {
        winner_hero_id: winner_id,
        loser_hero_id: loser_id,
        timestamp: ctx.epoch_timestamp_ms()
    });

    object::delete(id);
}

